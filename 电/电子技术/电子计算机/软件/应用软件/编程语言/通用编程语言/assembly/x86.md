# 简介

汇编语言笔记。<font color="red">在本文中，如果没有特殊说明，代码的都是基于 x86 指令集编写的</font>

# x86 或 AMD64 寄存器

可见寄存器：

| 8 位寄存器 | 16 位寄存器 | 32 位寄存器 | 64 位寄存器 | 描述                                                         |
| ---------- | ----------- | ----------- | ----------- | ------------------------------------------------------------ |
| AL、AH     | AX          | EAX         | RAX         | 通用寄存器                                                   |
| BL、BH     | BX          | EBX         | RBX         | 通用寄存器                                                   |
| CL、CH     | CX          | ECX         | RCX         | 通用寄存器                                                   |
| DL、DH     | DX          | EDX         | RDX         | 通用寄存器                                                   |
| SIL        | SI          | ESI         | RSI         | 使用 movs 指令族复制内存时，使用 SI 寄存器指定数据源的内存地址，默认段寄存器是 DS |
| SDL        | DI          | EDI         | RDI         | 使用 movs 指令族复制内存时，使用 DI 寄存器指定目标内存地址，默认段寄存器是 DS |
| BPL        | BP          | EBP         | RBP         | 用于存放当前函数栈的基准地址                                 |
| SPL        | SP          | ESP         | RSP         | 用于存放当前函数栈的边界地址                                 |
| CS         | CS          | CS          |             | 代码段寄存器                                                 |
| DS         | DS          | DS          |             | 数据段寄存器                                                 |
| SS         | SS          | SS          |             | 堆栈段寄存器                                                 |
| ES         | ES          | ES          |             | 拓展段寄存器                                                 |
| FS         | FS          | FS          |             | 拓展段寄存器                                                 |
| GS         | GS          | GS          |             | 拓展段寄存器                                                 |
|            | IP          | EIP         | RIP         | 指令寄存器，物理内存地址 = $\rm CS \times 16 + \rm IP$       |
| R8B        | R8W         | R8D         | R8          | 通用寄存器                                                   |
| R10B       | R10W        | R10D        | R9          | 通用寄存器                                                   |
| R11B       | R11W        | R11D        | R10         | 通用寄存器                                                   |
| R12B       | R12W        | R12D        | R12         | 通用寄存器                                                   |
| R13B       | R13W        | R13D        | R13         | 通用寄存器                                                   |
| R14B       | R14W        | R14D        | R14         | 通用寄存器                                                   |
| R15B       | R15W        | R15D        | R15         | 通用寄存器                                                   |

<font color="green">\*L 寄存器是 \*X 的低 8 位，\*H 是 \*X 的高 8 位。</font>

# Hello World

用汇编实现“Hello World”打印。

汇编器：[NASM](https://www.nasm.us/)

操作系统：Linux

CPU 架构：x86_64（64位）

本程序架构：x86（32位）

hello-world.asm:

```assembly
; 定义数据段，这是一个伪操作，用于告诉汇编器定义一块内存空间
section .data

; 定义数据段中的变量，这也是一个伪操作，所有对该变量的引用，都会被汇编器编译为内存地址
; DB 是伪指令，用于告诉汇编器，当前伪操作的每个操作数占用一个字节
; 0AH 用于写入一个换行符
STR_HELLO: DB "Hello World!", 0AH

; 计算字符串的长度
; equ 伪指令，表示操作数是一个表达式
; $ 应该是当前变量的地址
; 由于数据段的内存是连续的，当前变量地址 - 上一个变量地址 = 上一个变量的长度
STRLEN: equ $ - STR_HELLO

; 定义代码段，用于保存下面所有的汇编指令
section .text

; 定义程序入口，必须是“_start”
global _start
_start:
    ; 将变量 STRLEN 和 STR_HELLO 的地址分别放入寄存器 edx 和 ecx
    mov edx, STRLEN
    mov ecx, STR_HELLO

    ; 设置即将调用的 Linux 系统子功能号，4 表示 sys_write
    mov eax, 4
    ; 调用 Linux 的中断程序，开始执行指定的 Linux 子功能，当前进程暂停执行，由用户态转为内核态
    int 0x80

    ; 设置 Linux 系统子功能，1 表示 sys_exit
    mov eax, 1
    ; 设置进程的退出码
    mov ebx, 0
    ; 调用 Linux 的中断程序
    int 0x80

```

编译，以及生成可执行程序：

编译32位的程序：

```bash
# 使用 nasm 汇编器编译汇编
$ nasm -f elf hello-world.asm -o hello-world.o
# 使用链接器生成可执行程序
$ ld -m elf_i386 hello-world.o -o hello-world
```

编译64位的程序：

```bash
$ nasm -f elf64 hello-world.asm -o hello-world.o
$ ld hello-world.o -o hello-world
```

nasm 还支持第二种方式打印 hello world：

hello-world02.asm:

```assembly
global _start

section .data
message: db "Hello World", 10

section .text
_start:
    ; syscall for write
    mov rax, 1
    ; file handle 1 is stdout
    mov rdi, 1
    ; address of string to output
    mov rsi, message
    ; number of bytes
    mov rdx, 13
    ; invoke operating system to do the write
    syscall

    ; system call for exit
    mov rax, 60
    ; exit code 0
    mov rdi, 0
    ; invoke operating system to exit
    syscall
```

`syscall`只能作用于64位程序，暂时不知道原因，只能以64位模式编译程序：

```bash
$ nasm -f elf64 hello-world02.asm -o hello-world02.o
$ ld hello-world02.o -o hello-world02
```

# 函数调用

## call 的四种调用方式

* 相对近调用
    近调用：指调用的函数在同一个段中，无需跨段，即 cs 不变。
    相对：指待调用函数地址相对于下一条指令（即 call 的下一条），计算出一个偏移量。也就是说这个偏移量不是真正的段内偏移，而是相对位置偏移量。
    指令格式：`call near number`，默认是相对近调用。
    例如：假设目标函数地址是 0x9，相对的基准是 0x6，那么指令就是 `call near 0x03`。
* 间接绝对近调用
    间接：指不使用立即数作为函数地址，而是从寄存器或内存获得函数地址。
    绝对：得到的函数地址不进行相对量计算，直接跳转到该函数。
    例如：`mov [0x12345], 0x12345` 然后 `call near [0x12345]`
* 直接绝对远调用
    直接绝对：将操作数直接作为函数地址，不进行相对量计算。
    远调用：跨段访问，也就是 cs 和 ip 都需要改变。
    指令格式：`call far cs:ip`
    例如：`call far 0x10:0x99`
* 间接绝对远调用
    间接：函数的地址从内存读取，在内存中，前两个字节是段内偏移，后两个字节是段基址。
    例如：`call far [0x12345]`

## 过程调用

函数调用在汇编中，称为过程调用。汇编中的过程调用是通过指令 call 和 ret 完成的。call 指令将当前过程的返回地址压入堆栈，再把被调用过程的地址复制到指令指针寄存器。当过程准备返回时，它的 ret 指令从堆栈把返回地址弹回到 IP 寄存器。一个简单的示例如下：

```assembly
global _start

section .bss
BUFFER resb 2

section .data
STRLEN db 1

section .text
_start:
    mov byte [BUFFER], 97
    call demo
    mov byte [BUFFER + 1], 10

    ; 打印字符串
    mov edx, STRLEN
    mov ecx, BUFFER
    mov eax, 4
    int 0x80

    mov ebx, 0
    mov eax, 1
    int 0x80

demo:
    sub byte [BUFFER], 32
    ret
```

```bash
$ nasm -f elf demo.asm -o demo.o
$ ld -m elf_i386 demo.o -o demo
$ ./demo
A
```

## C 函数堆栈规范

在汇编中，其实并没有堆栈的概念。但是我们可以通过寄存器 ebp、esp 实现 C 函数的堆栈规范。C 函数堆栈主要有两个步骤：压栈、弹栈。

首先请看一段 C 代码：

```c
#include <stdio.h>

int sub(int a, int b) {
    int d;
    return a - b;
}

void _start() {
    printf("%c\n", demo(97, 32));
}
```

这段 C 代码的汇编对等实现

```assembly
global _start

section .bss
BUFFER resb 2

section .data
STRLEN db 1

section .text
_start:
    push ebp
    mov ebp, esp

    ; 将函数的参数压入堆栈，根据 C 的规范，需要从右往左依次压入参数
    ; 压入参数 b
    sub esp, 4
    mov dword [esp], 32
    ; 压入参数 a
    sub esp, 4
    mov dword [esp], 97
    call demo
    ; 回收 demo 函数的参数所占用的空间
    add esp, 8
    mov [BUFFER], eax
    mov byte [BUFFER + 1], 10

    ; 打印字符串
    mov edx, STRLEN
    mov ecx, BUFFER
    mov eax, 4
    int 0x80

    mov ebx, 0
    mov eax, 1
    int 0x80

demo:
    ; 创建栈帧
    ; 备份 ebp 的值
    sub esp, 4
    mov dword [esp], ebp
    mov ebp, esp
    ; 由于函数中存在局部变量 d，esp 需要给 d 腾出空间
    sub esp, 4

    ; 初始化 d
    mov dword [esp], 0
    ; 获取参数 a
    mov eax, [ebp + 8]
    ; 参数 a - 参数 b，函数返回值存放在 eax
    sub eax, [ebp + 12]

    ; 销毁栈帧
    ; esp 回归栈顶，销毁函数的局部变量占用的空间
    mov esp, ebp
    ; 恢复 ebp 的值，让 ebp 指向上一层堆栈的栈顶
    add esp, 4
    mov ebp, [esp]
    ret
```

```bash
$ nasm -f elf demo.asm -o demo.o
$ ld -m elf_i386 demo.o -o demo
$ ./demo
A
```

堆栈的整体布局如图所示：

![stack.svg](./assets/stack.svg)

x86 指令集提供三个指令简化压栈、弹栈操作：push、pop 和 leave：

```assembly
; 将操作数压入堆栈
push xxx
; 相当于
sub esp, 4
mov dword [esp], xxx
; 例如
push eax
push 0x1234
push [0x1234]

; 弹出堆栈，并将弹出的值保存到指定位置
pop xxx
; 相当于
add esp, 4
mov xxx, [esp]
; 例如
pop eax
pop [0x1234]

; 离开函数，销毁堆栈
leave
; 相当于
mov esp, ebp
pop ebp
```

对 esp 偏移量，应该取决于 CPU 的架构，16 位 CPU 偏移量 2，32 位 CPU偏移量 4，64 位 CPU 偏移量 8。

## 外部过程调用

除了调用同一个文件中的过程之外，还可以通过 ld 调用外部过程：

main.asm:

```assembly
; 声明此函数在别的文件中
extern print
; 声明程序入口，只能是 _start
global _start

; 自定义数据段，未使用“传统”的数据段 .data
section .data
; 在数据段中存放一个字符串，OA 是一个换行符
strHello db "Hello World!", 0AH
STRLEN equ $ - strHello

section .text
_start:
    ; 创建栈帧
    push ebp
    mov ebp, esp

    ; 传入参数，待打印的字符串
    push strHello
    ; 传入参数，字符串的长度
    push STRLEN
    ; 调用外部函数
    call print
    ; 回收 print 函数的参数占用的空间
    add esp. 8

    ; 函数调用完毕，退出程序
    ; 设置程序返回值（exit code）
    mov ebx, 0
    ; 设置系统子功能调用号，1: sys_exit
    mov eax, 1
    ; 触发中断，执行系统调用
    int 0x80
```

print.asm:

```assembly
; 导出 print，供其他模块使用
global print

; 自定义代码段
section .text
print:
    ; 创建栈帧
    ; 备份上一层函数的堆栈地址
    push ebp
    ; 将 ebp 更新为当前对战地址
    mov ebp, esp

    ; 字符串长度
    mov edx,[ebp+8]
    ; 设置要输出的字符串
    mov ecx,[ebp+12]

    ; 设置系统子功能号，4:sys_write
    mov eax, 4
    ; 开始系统调用
    int 0x80

    ; 即将离开函数，销毁栈帧
    leave
    ret
```

编译汇编，并生成可执行程序：

```bash
$ nasm -f elf64 main.asm -o main.o && nasm -f elf print.asm -o print.o
$ ld main.o print.o -o main
```

# mov

```assembly
; 未初始化的内存必须放在节 .bss 当中
section .bss
; 定义一块未初始化的内存
CACHE: resb 10
CACHE_LEN: equ $ - CACHE
BUFFER01: resb 8192
BUFFER02: resb 8192

section .data
MESSAGE: db "Hello World", 10
MESSAGE_LEN: equ $ - MESSAGE

section .text

; 打印内存中的内容
print:
    push ebp
    mov ebp, esp

    mov edx, [esp + 8]
    mov ecx, [esp + 12]
    mov eax, 4
    int 0x80

    leave
    ret

global _start
_start:
    push ebp
    mov ebp, esp

    ; 向指定的内存地址写入一些数据，[...] 表示操作数是一个内存地址
    ; byte 表示写入内存的数据大小是一个字节
    mov byte [CACHE], 97
    mov byte [CACHE + 1], 'a'
    mov byte [CACHE + 2], 10
    push CACHE
    push CACHE_LEN
    call print
    add esp, 8

    ; 通过减法，将一个字母转为大写字母
    mov eax, [CACHE]
    sub eax, 32
    mov [CACHE], eax
    push CACHE
    push CACHE_LEN
    call print
    add esp, 8

    ; 通过 mov 指令复制内存
    ; 设置源地址
    mov esi, MESSAGE
    ; 设置目标地址
    mov edi, BUFFER01
    ; 使用 ecx 寄存器进行循环计数
    mov ecx, MESSAGE_LEN
    cycle:
        mov ah, [esi]
        mov [edi], ah
        add esi, 1
        add edi, 1
        sub ecx, 1
        cmp ecx, 0
        ; 如果 ecx > 0，继续复制剩下的内容
        ja cycle
    push BUFFER01
    push MESSAGE_LEN
    call print
    add esp, 8

    ; 通过 movsb 指令族复制内存
    ; 设置源内存地址，段寄存器是 DS
    mov esi, MESSAGE
    ; 设置源内存地址，段寄存器是 ES
    mov edi, BUFFER02
    ; 设置要复制的内存大小
    mov ecx, MESSAGE_LEN
    ; 开始复制内存
    ; rep 是重复执行指定的指令，重复次数通过 ecx 指定
    ; movsb 是每次复制一个字节，并且 esi 和 edi 自动加 1
    rep movsb
    push BUFFER02
    push MESSAGE_LEN
    call print
    add esp, 8

    ; 退出程序
    mov eax, 1
    mov ebx, 0
    int 0x80
```

<font color="red">注意：</font>

<font color="red">如果需要从寄存器获取内存地址，在实模式下，mov 只能从 BX、SI、DI 或 EP 寄存器获取内存地址，在保护模式下，mov 可以从任意通用寄存器获取内存地址。</font>

# 内存大小和操作数大小定义

| 内存大小声明 | 操作数大小声明 | 大小（单位：字节） |
| ------------ | -------------- | ------------------ |
| db           | byte           | 1                  |
| dw           | word           | 2                  |
| dd           | dword          | 4                  |
| dq           | qword          | 8                  |

# 调用 C 函数

以调用 C 的 printf 函数为例，代码如下：

```assembly
; 从 nasm 汇编调用 C 的 printf 函数

global main
extern printf, exit

section .data
FORMAT: db "%s", 10, 0
MESSAGE: db "Hello World", 0

section .text
main:
    push ebp

    push MESSAGE
    push FORMAT
    call printf
    add esp, 8

    push 0
    call exit
```

编译程序：

```bash
$ nasm -f elf demo.asm -o demo.o
$ gcc -no-pie -m32 demo.o -o demo
```

在 64 位程序中，函数的参数传递是优先存放在 6 个寄存器 rdi、rsi、rdx、rcx、r8、r9 当中，因此，64 位 printf 的参数传递也必须使用寄存器：

```assembly
; 64 位的 printf 使用寄存器 rdi、rsi、rdx、rcx、r8、r9 传递参数

global main
extern printf, exit

section .data
FMT db "%s", 10, 0
MESSAGE db "Hello World"

section .text
main:
    push rbp

    mov rdi, FMT
    mov rsi, MESSAGE
    call printf

    mov rdi, 0
    call exit
```

```bash
$ nasm -f elf64 demo.asm -o demo.o
# PIE 是用于可执行文件中的地址空间随机化的技术，PIC 是对地址空间通过偏移量进行相对定位，-no-pie 表示不使用 PIE，只使用 PIC
$ gcc -no-pie demo.o -o demo
```

无论是 32 位程序，还是 64 位程序，函数的返回值都是放在 eax 中。

在 64 位程序中，如果函数的参数的数量超过 6 个，剩余的参数还是会通过 push 压入堆栈的。

# 指令转移

## 无条件指令转移

无条件的指令转移主要是通过 `jmp` 指令完成的。

### 相对短转移

是否段内转移：是

指令格式：

```assembly
jmp short laber
```

操作数大小：1 B

操作数取值范围：-128 ~ 127

目标地址计算公式：当前指令地址 + 当前指令大小 + 相对量（操作数）

例子：

```assembly
jmp $
```

当前指令的结束地址减去 2 等于当前指令的开始地址，重新执行当前指令。这就是一个死循环。

<font color="orange">注意：</font>

<font color="orange">如果操作数是立即数，生成的操作码只能在实模式中执行。原因未知。</font>

### 16 位相对近转移

是否段内转移：是

指令格式：

```assembly
jmp near register
jmp near [memory_address]
```

### 16 位直接绝对转移

执行时，处理器用段地址的内容取代 CS 的值，用偏移地址（也可以是标号）取代 IP 的值。

指令格式：

```assembly
jmp segment_address: offset_address
```

### 16 位间接绝对转移

执行时，处理器根据内存地址找到偏移地址和段地址，分别用来代替IP和CS的内容。内存中，存放着两个字节，低字节是偏移地址，高字节是段地址。

指令格式：

```assembly
jmp far [memory_address]
```

内存布局：

```text
offset   segment
00000000 00000000
```

## 条件转移指令

条件转移指令为高级语言的 if、while 等提供了支持。条件转移指令需要配合[ Flags 寄存器](#Flags 寄存器)才能发挥它的作用。所有的条件转移指令如下：

| 指令        | 条件               | 意义                                | 英文助记                                |
| ----------- | ------------------ | ----------------------------------- | --------------------------------------- |
| jz/je       | ZF = 1             | 相减结果等于 0 时转移，或相等时转移 | Jump if Zero/Equal                      |
| jnz/jne     | ZF = 0             | 不等于 0，或不相等时转移            | Jump if not Zero/Not Equal              |
| js          | SF = 1             | 负数时转移                          | Jump if sign                            |
| jns         | SF = 0             | 非负数时转移                        | Jump if not sign                        |
| jo          | OF = 1             | 溢出时转移                          | Jump if Overflow                        |
| jno         | OF =0              | 未溢出时转移                        | Jump if not overflow                    |
| jp/jpe      | PF = 1             | 低字节中，有偶数个 1 时转移         | Jump if parity even                     |
| jnp/jpo     | PF = 0             | 低字节中，有奇数个 1 时转移         | Jump if not parity odd                  |
| jbe/jna     | CF = 1 或 ZF = 1   | 小于等于/不大于时转移               | Jump if below of equal/not above        |
| jnbe/ja     | CF = 0 或 ZF =0    | 不小于等于/大于时转移               | Jump if not below or equal/above        |
| jc/jb/jnae  | CF = 1             | 进位/小于/不大于等于时转移          | Jump if Carry/Below/Not Above Equal     |
| jnc/jnb/jae | CF = 0             | 未进位/不小于/大于等于时转移        | Jump if Not Carry/Not Below/Above Equal |
| jl/jnge     | SF != OF           | 小于/不大于等于时转移               | Jump Less/Not Great Equal               |
| jnl/jge     | SF = OF            | 不小于/大于等于时转移               | Jump if Not Less/Great Equal            |
| jle/jng     | ZF != OF 或 ZF = 1 | 小于等于/不大于时转移               | Jump if Less or Equal/Not Great         |
| jnle/jg     | SF = OF 且 ZF = 0  | 不小于等于/大于时转移               | Jump Not Less Equal/Great               |
| Jcxz        | CX 寄存器值为 0    | CX 寄存器值为 0 时转移              | Jump if register CX's value is 0        |

在大多数情况下，在条件指令之前，会存在一条或多条比较指令，比如 cmp 指令。

例：比较寄存器 eax、ebx 的值：

```assembly
global main
extern printf, exit

section .data
FORMAT: db "%s", 10, 0
EQUAL_MSG: db "eax == ebx", 0
LESS_MSG: db "eax < ebx", 0
GREATER_MSG: db "eax > ebx", 0
ELSE_MSG: db "None of the conditions were hit.", 0

section .text
main:
    push ebp

    mov eax, 100
    mov ebx, 1
    ; cmp 是比较指令，它将目标操作数减去源操作数，并根据结果，修改 Flags 寄存器。它不会修改任何操作数，即便该操作数是寄存器或内存地址
    cmp eax, ebx

    ; eax == ebx 时转移
    je equal
    ; eax < ebx 时转移
    jl less
    ; eax > ebx 时转移
    ja greater
    ; 所有条件都不满足时，进行无条件转移
    jmp else

equal:
    push EQUAL_MSG
    push FORMAT
    call printf
    add esp, 8
    jmp end

less:
    push LESS_MSG
    push FORMAT
    call printf
    add esp, 8
    jmp end

greater:
    push GREATER_MSG
    push FORMAT
    call printf
    add esp, 8
    jmp end

else:
    push ELSE_MSG
    push FORMAT
    call printf
    add esp, 8
    jmp end

end:
    push 0
    call exit
```

<font color="orange">注意事项：</font>

<font color="orange">jl 和 jb 都是小于时跳转，但是 jb 要求进行比较的两个数的符号必须相同，要么都是正数，要么都是负数，不能是一正一负。jl 无此限制。</font>

# Flags 寄存器

> x86 架构 CPU 中，标志寄存器主要有3种作用：
>
> - 存储相关指令执行后的结果，例如 CF、PF、AF、ZF、OF 标志位
> - 执行相关指令时，提供行为依据，例如执行 JE 指令时会读取 ZF 的值，来决定是否进行跳转。
> - 控制 CPU 的工作方式，例如I F、VM、TF 等标志位。
>
> 16 位模式下，标志寄存器名称为  FLAG，寄存器大小 16 位。
> 32 位模式下，标志寄存器的名称为  EFLAG，寄存器大小  32 位。
> 64 位模式下，为 RFLAG，寄存器大小 64 位。

| 00   |   01 | 02   | 03   | 04   | 05   | 06   | 07   | 08   | 09   | 10   | 11   | 12   | 13   | 14   | 15   | 16   | 17   | 18   | 19   | 20   | 21   | 22   | 23   | 24   | 25   | 26   | 27   | 28   | 29   | 30   | 31   |
| ---- | ---: | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- | ---- |
| CF   |    - | PF   | -    | AF   | -    | ZF   | SF   | TF   | IF   | DF   | OF   | IOPL | IOPL | NT   | -    | RF   | VM   | AC   | VIF  | VIP  | ID   | -    | -    | -    | -    | -    | -    | -    | -    | -    | -    |

“-”表示保留，只用于占位，或者将来拓展。

CF：Carry Flag。运算中，数值中的最高位有可能是进位或借位，不管是进位，还是借位，CF 都会设置为 1，否则为 0。它可用于检测无符号数加减法是否有溢出。因为 CF 为 1 时，也就是最高位有进位或借位，肯定是溢出。

PF：Parity Flag，奇偶位。用于标记结果低 8 位中 1 的个数，如果为偶数，PF 为 1，否则为 0。经常用于数据传输开始时和结束后的对比，判断传输过程中是否出现错误。

AF：Auxiliary carry Flag。辅助进位标志，用来记录运算结果低 4 位的进、借位情况，若低半字节有进位、借位，AF 为 1，否则为 0。

ZF：Zero Flag。零标志位，若计算结果为 0，此标志为 1，否则为 0。

SF：Sign Flag。符号标志位。若运算结果为负，则 SF 为 1，否则为 0。

TF：Trap Flag。陷阱标志位，如果为 1，CPU 进入单步运行，若为 0，则为连续工作的方式。平时用的 debug，在单步调试时，原理上就是让 TF 位为 1。

IF：Interrupt Flag。中断标志位。如果 IF 为 1，表示中断开启，CPU 可以响应外部可屏蔽中断。若为 0，表示中断关闭，CPU 不再响应来自外部的[可屏蔽中断](https://baike.baidu.com/item/%E5%8F%AF%E5%B1%8F%E8%94%BD%E4%B8%AD%E6%96%AD/2472869)，但 CPU 内部的异常还是要响应的。

DF：Direction Flag。方向标志位。用于字符串操作指令中，当 DF 为 1 时，指令中的操作数地址会自动减少一个单位，当 DF 为 0 时，指令中的操作数会自动增加一个单位。

OF：Overflow Flag。用来标识计算的结果是否超过了数据类型可表示的范围。若为 1 则表示有溢出，为 0 则表示没有发生溢出。

IOPL：Input Output Privilege Level。输入输出权限等级标志位。有四个任务特权级：0 ~ 3。

NT：Nest Task。任务嵌套标志位。8088 支持多任务，一个任务就是一个进程。当一个任又嵌套调用了另一个任务（进程）时，此 NT 位为 1，否则为 0。

RF：Resume Flag。恢复标志位。该标志位用于程序调试，只是是否接受调试故障，它需要与调试寄存器一起使用。当 RF 为 1 时忽略调试故障，为 0 时接受调试故障。

VM：Virtual 8086 Model。虚拟 8086 模式标志位。实模式向保护模式过度时的产物。CPU 有了保护模式后，为了兼容实模式下的程序，允许将此位设置为 1，以便在保护模式下运行实模式的程序。实模式下的程序不支持多任务，而且程序中的地址就是真实的物理地址。所以在保护模式下，每运行一个实模式下的程序，就要为其虚拟一个实模式环境。

AC：Alignment。对齐检查标志位。对齐是指程序中的数据或内存地址是否是偶数，是否是 16、32 的倍数，这样硬件每次对地址一自增的方式（每次自加 2、16、32）访问内存时，自增后的地址正好对齐数据所在的起始地址上。对齐并不是软件逻辑中的要求，而是硬件上的偏好，<font color="green">如果待访问的内存地址是 16 或 32 的整数倍，硬件上好处理，所以运行较快。</font>若 AC 位为 1 时，则进行地址对齐检查，为 0 时不检查。

VIF：Virtual Interrupt Flag。虚拟中断标志位。虚拟模式下的中断标志。

VIP：Virtual Interrupt Pending。虚拟中断挂起标志位。在多任务情况下，为操作系统提供的虚拟中断挂起信息，需要与 VIF 配合使用。

ID：Identification。识别标志位。系统经常要判断 CPU 型号，若 ID 为 1，表示当前 CPU 支持 ID 指令，这样便能获取 CPU 的型号、厂商等信息。若 ID 为 0，则表示当前 CPU 不支持 ID 指令。

# 16 bit、32 bit 指令的编译

nasm 默认是生成 16 bit 的操作码，如果想要生成指定大小的的操作码需要通过 bits 伪指令进行定义。例如，生成 16 bit 的操作码：`[bits 16]`，生成 32 bit 的操作码：`[bits 32]`。

运行模式反转是为了在 32 位保护模式中，兼容实模式的程序而诞生的产物。请先看如下代码：

bits16.asm:

```assembly
[bits 16]
mov ax, 0
mov eax, 0
mov word [bx], 0
mov word [eax], 0
```

bits32.asm:

```assembly
[bits 32]
mov ax, 0
mov eax, 0
mov word [bx], 0
mov word [eax], 0
```

编译后查看生成的操作码：

```bash
$ nasm bits16.asm -o bits16.bin
$ nasm bits32.asm -o bits32.bin
$ ndisasm -b 16 bits16.bin
00000000  B80000            mov ax,0x0
00000003  66B800000000      mov eax,0x0
00000009  C7070000          mov word [bx],0x0
0000000D  67C7000000        mov word [eax],0x0
$ ndisasm -b 32 bits32.bin
00000000  66B80000          mov ax,0x0
00000004  B800000000        mov eax,0x0
00000009  6667C7070000      mov word [bx],0x0
0000000F  66C7000000        mov word [eax],0x0
```

0x66 是操作数反转前缀，0x67 是寻址方式反转前缀。0x66 的作用是在 16 位指令中，将操作数反转为 32 位，或者在 32 位指令中，将操作数反转为 16 位。0x67 也同样是反转前缀，它反转的是寻址方式。

# 常用指令

## cmpxchg

指令格式：`cmpxchg dest, src`

指令作用：将操作数 dest 的值与 rax 寄存器中的值进行比较，如果相等，则设置 ZF 标志位，并将操作数  src 的值保存到操作数 dest 中。

## lock

lock 不是一个单独的指令，它是一个指令前缀。

指令格式：`lock instruction`

指令作用：lock 指令前缀用于确保一条指令的执行是原子的，它的原理是锁定一个特定的内存地址，组织其他 CPU 内核对该地址的访问。

样例：`lock cmpxchg [XXX], rbx`
